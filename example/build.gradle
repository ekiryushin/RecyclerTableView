plugins {
    id 'com.android.application'
    id 'kotlin-android'
}

Properties properties = new Properties()
properties.load(rootProject.file("local.properties").newDataInputStream())

android {
    compileSdk rootProject.ext.targetSdk

    defaultConfig {
        applicationId "${rootProject.ext.groupId}.${rootProject.ext.artifactId}.example"
        minSdk rootProject.ext.minSdk
        targetSdk rootProject.ext.targetSdk
        versionCode rootProject.ext.versionCode
        versionName rootProject.ext.exampleVersionName
    }

    signingConfigs {
        release {
            //путь до файла подписи
            def keystorePath = rootProject.file("local.properties").exists()
                    ? properties.getProperty("app.sign.file")
                    : null

            //файл подписи
            def keystoreFile = (keystorePath != null && !keystorePath.isEmpty())
                    ? file(keystorePath)
                    : null

            if (keystoreFile != null) {
                storeFile keystoreFile
                storePassword properties.getProperty("app.sign.filePassword")
                keyAlias properties.getProperty("app.sign.alias")
                keyPassword properties.getProperty("app.sign.aliasPassword")
            }
        }
    }

    buildTypes {
        release {
            signingConfig signingConfigs.release?.storeFile
                    ? signingConfigs.release
                    : signingConfigs.debug
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    kotlinOptions {
        jvmTarget = '1.8'
    }

    buildFeatures {
        viewBinding true
    }
}

dependencies {

    //implementation "com.github.ekiryushin:RecyclerTableView:1.0.1"
    //implementation 'com.github.ekiryushin:RecyclerTableView:1.0.1-SNAPSHOT'
    implementation project(':recyclertableview')

    implementation 'androidx.core:core-ktx:1.7.0'
    implementation 'com.google.android.material:material:1.3.0'
    implementation 'androidx.paging:paging-runtime-ktx:3.1.1'
}