apply plugin: 'maven-publish'
apply plugin: 'signing'

tasks.register("sourceJar", Jar) {
    from android.sourceSets.main.kotlin.srcDirs
    archiveClassifier.set('sources')
    description = "Исходный код библиотеки"
    group = JavaBasePlugin.DOCUMENTATION_GROUP
}

artifacts {
    archives sourceJar
}

Properties properties = new Properties()
properties.load(rootProject.file("local.properties").newDataInputStream())

project.ext["signing.keyId"] = properties.getProperty("lib.sign.keyId")
project.ext["signing.secretKeyRingFile"] = properties.getProperty("lib.sign.secretKeyRingFile")
project.ext["signing.password"] = properties.getProperty("lib.sign.password")

afterEvaluate {
    publishing {
        publications {
            release(MavenPublication) {
                groupId = rootProject.ext.groupId
                artifactId = rootProject.ext.artifactId
                version = rootProject.ext.recyclertableviewVersionName

                from components.release

                artifact sourceJar

                pom {
                    name = 'RecyclerTableView'
                    description = 'RecyclerTableView - Android-библиоткека для отображения ' +
                            'данных в виде прокручиваемой таблицы на основе RecyclerView. ' +
                            'Данные для таблицы могут формироваться сразу или постранично.'
                    url = 'https://github.com/ekiryushin/RecyclerTableView'

                    licenses {
                        license {
                            name = 'The Apache License, Version 2.0'
                            url = "http://www.apache.org/licenses/LICENSE-2.0.txt"
                        }
                    }

                    developers {
                        developer {
                            id = 'ekiryushin'
                            name = 'Kiryushin Eugene'
                            email = 'kiryushin.ea@gmail.com'
                        }
                    }

                    scm {
                        connection = 'scm:git:github.com/ekiryushin/RecyclerTableView.git'
                        developerConnection = 'scm:git:ssh://github.com/ekiryushin/RecyclerTableView.git'
                        url = 'https://github.com/ekiryushin/RecyclerTableView'
                    }
                }
            }
        }

        repositories {
            maven {
                url = 'https://jitpack.io'
                credentials {
                    username = properties.getProperty("lib.jitpack.username")
                    password = properties.getProperty("lib.jitpack.token")
                }
            }
        }
    }

    signing {
        required { !System.getenv('CI') && rootProject.file('local.properties').exists() }

        sign(publishing.publications)
    }
}